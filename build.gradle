ext {
    bintrayRepo = 'chrynan'
    libraryGroup = 'com.chrynan'
    libraryOwner = 'chrynan'
    libraryRepo = 'uri'
    libraryVersion = '0.1.0'
    libraryVersionCode = 1
    libraryVersionDescription = 'Minor Release'
    libraryGithubUrl = 'https://github.com/chRyNaN/uri.git'
    libraryLicense = 'Apache-2.0'
}

group rootProject.libraryVersion
version rootProject.libraryVersion

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

kotlin {
    targets {
        jvm()
        js()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
    }
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "uri-core"
        }
        jvm {
            artifactId = "uri-core-jvm"
        }
        js {
            artifactId = "uri-core-js"
        }
        metadata {
            artifactId = "uri-core-metadata"
        }
    }
}

bintray {
    user = project.findProperty("BINTRAY_USER") ?: System.getenv('BINTRAY_USER')
    key = project.findProperty("BINTRAY_KEY") ?: System.getenv('BINTRAY_KEY')
    pkg {
        repo = rootProject.bintrayRepo
        name = rootProject.libraryRepo
        licenses = [rootProject.libraryLicense]
        vcsUrl = rootProject.libraryGithubUrl
        version {
            name = rootProject.libraryVersion
            desc = rootProject.libraryVersionDescription
            released = new Date()
            vcsTag = rootProject.libraryVersion
        }
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }
}

bintrayUpload.dependsOn publishToMavenLocal
